plugins {
  id 'com.gradle.plugin-publish' version '0.9.7'
  id 'io.codearte.nexus-staging' version '0.8.0'
  id 'maven'
  id 'signing'
}

group = "com.akamai"
version = "0.2.0"

repositories {
  mavenCentral()
}

dependencies {
  compile gradleApi()

  testCompile 'junit:junit:4.12'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
  required {
      gradle.taskGraph.hasTask('uploadArchives')
  }
  sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'EdgeAuth-Token-Java'
        groupId 'com.akamai'
        artifactId 'edgeauth'
        packaging 'jar'
        description 'Akamai Edge Authorization Token for Java'
        url 'https://github.com/akamai/EdgeAuth-Token-Java'

        scm {
          connection 'scm:git@github.com:akamai/EdgeAuth-Token-Java.git'
          developerConnection 'scm:git@github.com:akamai/EdgeAuth-Token-Java.git'
          url 'scm:git@github.com:akamai/EdgeAuth-Token-Java.git'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'astin'
            name 'Astin Choi'
          }
        }
      }
    }
  }
}

nexusStaging {
    packageGroup = "com.akamai"
}

pluginBundle {
  website = 'https://github.com/akamai/EdgeAuth-Token-Java'
  vcsUrl = 'https://github.com/akamai/EdgeAuth-Token-Java'
  description = 'Akamai Authorization Token for Java'
  tags = ['akamai', 'akamai-open', 'authorization', 'token', 'edgeauth', 'signed-url', 'java']

  plugins {
    edgeauthPlugin {
      id = 'com.akamai.edgeauth'
      displayName = 'Akamai Edge Authorization Token for Java'
    }
  }
}